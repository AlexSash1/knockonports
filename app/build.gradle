/*
 * Copyright (c) 2018 Alexander Yaburov 
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'com.github.triplet.play'

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def publisherPropertiesFile = rootProject.file("publisher.properties")

def publisherProperties = new Properties()

if (publisherPropertiesFile.exists()) {
    publisherProperties.load(new FileInputStream(publisherPropertiesFile))
}

android {
    compileSdkVersion 28

    if (!publisherProperties.isEmpty()) {
        playAccountConfigs {
            defaultAccountConfig {
                serviceAccountEmail = publisherProperties["email"]
                jsonFile = file(publisherProperties["key"])
            }
        }
    }

    defaultConfig {
        applicationId "me.impa.knockonports"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 9
        versionName "1.0.6"
        vectorDrawables.useSupportLibrary true
        externalNativeBuild {
            cmake {
                cppFlags ""
                //arguments "-DANDROID_STL=c++_shared"
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        if (!publisherProperties.isEmpty()) {
            playAccountConfig = playAccountConfigs.defaultAccountConfig
        }
    }
    if (!keystoreProperties.isEmpty()) {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (!keystoreProperties.isEmpty()) {
                signingConfig signingConfigs.release
            }
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/CMakeLists.txt')
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.11"

    implementation "org.jetbrains.anko:anko-commons:0.10.5"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation "com.github.savvyapps:ToggleButtonLayout:1.2.0"
    implementation "androidx.room:room-runtime:2.1.0-alpha02"
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "com.jaredrummler:colorpicker:1.0.2"
    implementation "com.github.hendrawd:StorageUtil:1.0.0"
    kapt "androidx.room:room-compiler:2.1.0-alpha02"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
}

play {
    track = 'production'
    //uploadImages = true
}
