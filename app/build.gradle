/*
 * Copyright (c) 2018 Alexander Yaburov 
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlin-kapt")
    id("com.github.triplet.play") version "3.4.0-agp4.2" apply false
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def publisherPropertiesFile = rootProject.file("publisher.properties")

def publisherProperties = new Properties()

if (publisherPropertiesFile.exists()) {
    publisherProperties.load(new FileInputStream(publisherPropertiesFile))
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "me.impa.knockonports"
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 46
        versionName "1.1.6b"
        vectorDrawables.useSupportLibrary true
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared"
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    if (!keystoreProperties.isEmpty()) {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (!keystoreProperties.isEmpty()) {
                signingConfig signingConfigs.release
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/CMakeLists.txt')
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.10"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.4.0-beta01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation "androidx.room:room-ktx:2.3.0"
    implementation "androidx.room:room-runtime:2.3.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.paging:paging-runtime-ktx:3.0.0"
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "com.jaredrummler:colorpicker:1.0.2"
    implementation "com.github.hendrawd:StorageUtil:1.0.0"
    implementation "com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3"

    annotationProcessor "androidx.room:room-compiler:2.3.0"

    kapt "androidx.room:room-compiler:2.3.0"
}

if (!publisherProperties.isEmpty()) {
    apply plugin: 'com.github.triplet.play'
    play {
        //serviceAccountEmail = publisherProperties["email"]
        serviceAccountCredentials = file(publisherProperties["key"])
        track = 'production'
        defaultToAppBundles = true
        //uploadImages = true
    }
}
